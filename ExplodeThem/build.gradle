apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        targetSdkVersion 23
        versionCode 7
        versionName "1.4"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        free {
            minSdkVersion 14

            applicationId "org.fruct.oss.explodethem.free"
            versionName "1.4-free"

            if (file("private.properties").exists()) {
                def props = new Properties()
                props.load(new FileInputStream(file("private.properties")));
                resValue "string", "banner_id", props["banner_id"]
                resValue "string", "test_devices", props["test_devices"]

                resValue "string", "adwow_key", props["adwow_key"]
                resValue "string", "adwow_secret", props["adwow_secret"]
                logger.info("banner_id found in private.properties")
            } else {
                logger.warn("No banner_id defined in private.properties")
            }
        }

        full {
            minSdkVersion 9

            applicationId "org.fruct.oss.explodethem.full"
            versionName "1.4-full"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    //compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    freeCompile files("libs/AdWoW_SDK_1.0.6.jar")
    freeCompile 'com.google.android.gms:play-services-ads:8.4.0'
}
